apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'realm-android'
apply plugin: 'com.google.firebase.firebase-crash'

def versionMajor = 0
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0

buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
  }
}

repositories {
  maven { url 'https://maven.fabric.io/public' }
  maven { url "https://jitpack.io" }
  mavenCentral()
}

android {
  signingConfigs {
    release
  }
  def props = new Properties()

  props.load(new FileInputStream(rootProject.file("signing.conf")))
  android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
  android.signingConfigs.release.storePassword props.keyStorePassword
  android.signingConfigs.release.keyAlias props.keyAlias
  android.signingConfigs.release.keyPassword props.keyAliasPassword

  compileSdkVersion rootProject.compileSdkVersion
  buildToolsVersion rootProject.buildToolsVersion
  defaultConfig {
    applicationId "asvid.counter"
    minSdkVersion rootProject.minSdkVersion
    targetSdkVersion rootProject.targetSdkVersion
    versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
    versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"
    multiDexEnabled false
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    vectorDrawables.useSupportLibrary = true
  }
  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      zipAlignEnabled true
    }
  }
  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }
  lintOptions {
    abortOnError false
  }
}

dependencies {
  compile fileTree(include: ['*.jar'], dir: 'libs')
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "com.android.support:appcompat-v7:$supportLibraryVersion"
  compile "com.android.support:preference-v7:$supportLibraryVersion"
  compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
  compile "com.android.support:cardview-v7:$supportLibraryVersion"
  compile "com.android.support:design:$supportLibraryVersion"
  compile 'com.google.firebase:firebase-core:10.2.0'
  compile 'com.google.firebase:firebase-crash:10.2.0'
  compile 'com.google.firebase:firebase-messaging:10.2.0'
  //  color picker
  compile 'com.thebluealliance:spectrum:0.7.1'
  //  colors for color picker
  compile 'com.wada811:android-material-design-colors:3.0.0'
  //  logging
  compile 'com.jakewharton.timber:timber:4.5.1'
  //  vector icons
  compile 'com.mikepenz:iconics-core:2.8.1@aar'
  compile 'com.mikepenz:fontawesome-typeface:4.6.0.3@aar'
  //  charts
  compile 'com.github.PhilJay:MPAndroidChart:v3.0.1'
  //  pretty time
  compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'

  testCompile 'junit:junit:4.12'
  debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
  releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
  compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'
}

apply plugin: 'com.google.gms.google-services'